buildscript {
  repositories {
    mavenCentral()
  }
}

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'java'

    group = 'org.reparationservice'
    version = '0.0.1-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    eclipse {
        classpath {
            containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
            containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
    	compile 'joda-time:joda-time:2.6'
        testCompile 'org.assertj:assertj-core:2.0.0'
        testCompile 'de.bechte.junit:junit-hierarchicalcontextrunner:4.12.0'
    }
    //Expose project test classes
    task testJar(type: Jar, dependsOn: testClasses) {
        baseName = "test-${project.archivesBaseName}"
        from sourceSets.test.output
    }
    //testCompile project(path: ':aProject', configuration: 'tests')
    configurations {
        tests
    }
    artifacts {
        tests testJar
    }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.3'
}
